# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.15
pkgbase=mate-settings-daemon
pkgname=(${pkgbase})
pkgver=${_ver}.0
pkgrel=1
pkgdesc="The MATE Settings daemon"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'fontconfig' 'glib2' 'gtk3' 'gtk-update-icon-cache'
         'libcanberra-pulse' 'libmatekbd>=1.15' 'libmatemixer' 'libnotify'
         'libxt' 'mate-desktop>=1.15' 'nss' 'polkit')
makedepends=('mate-common>=1.15' 'gtk3' 'libmatekbd>=1.15' 'mate-desktop>=1.15')
groups=('mate')
replaces=("${pkgbase}-gstreamer" "${pkgbase}-pulseaudio")
conflicts=("${pkgbase}-gtk3" "${pkgbase}-gstreamer" "${pkgbase}-pulseaudio")
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('563c6b50669cde375e333aaba1f1919351372871')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=3.0 \
        --enable-polkit \
        --disable-static
    make
}

package_mate-settings-daemon() {
    pkgdesc+=' (GTK3 version)'
    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
