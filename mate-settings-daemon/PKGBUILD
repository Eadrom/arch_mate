# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.14
pkgbase=mate-settings-daemon
pkgname=(${pkgbase} ${pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="The MATE Settings daemon"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('mate-common>=1.14' 'glib2' 'gtk2' 'gtk3' 'dbus-glib' 'mate-desktop>=1.14'
             'libmatekbd>=1.14' 'libxklavier' 'libmatemixer>=1.14' 'libcanberra-pulse'
             'nss' 'libnotify')
groups=('mate')
replaces=("${pkgbase}-gstreamer" "${pkgbase}-pulseaudio")
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('efb8782cbf68035613b2104e3b2197e96f6fcb03')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk2"
    cp -a "${pkgbase}-gtk2" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk2"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=2.0 \
        --enable-polkit \
        --disable-static
    make

    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
		--prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/${pkgname} \
		--sysconfdir=/etc \
		--with-gtk=3.0 \
		--enable-polkit \
		--disable-static
    make
}

package_mate-settings-daemon() {
    pkgdesc+=' (GTK2 version)'
    conflicts=("${pkgbase}-gtk3" "${pkgbase}-gstreamer" "${pkgbase}-pulseaudio")
    depends=('dbus-glib' 'dconf' 'fontconfig' 'glib2' 'gtk2' 'gtk-update-icon-cache'
             'libcanberra-pulse' 'libmatekbd>=1.14' 'libmatemixer>=1.14' 'libnotify'
             'libxt' 'mate-desktop>=1.14' 'nss' 'polkit')
    provides=('mate-settings-daemon=1.14.0')

    cd "${srcdir}/${pkgbase}-gtk2"
    make DESTDIR="${pkgdir}" install
}

package_mate-settings-daemon-gtk3() {
    pkgdesc+=' (GTK3 version)'
    conflicts=("${pkgbase}" "${pkgbase}-gstreamer" "${pkgbase}-pulseaudio")
    depends=('dbus-glib' 'dconf' 'fontconfig' 'glib2' 'gtk3' 'gtk-update-icon-cache'
             'libcanberra-pulse' 'libmatekbd>=1.14' 'libmatemixer>=1.14' 'libnotify'
             'libxt' 'mate-desktop>=1.14' 'nss' 'polkit')
    provides=('mate-settings-daemon=1.14.0')

    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
