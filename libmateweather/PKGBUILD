# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.15
_pkgbase=libmateweather
pkgname="${_pkgbase}"
pkgver=${_ver}.0
pkgrel=1
pkgdesc="Provides access to weather information from the Internet."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
provides=("${_pkgbase}" "${_pkgbase}-gtk3")
conflicts=("${_pkgbase}-gtk3")
license=('LGPL')
depends=('dconf' 'gtk3' 'libsoup' 'glib2' 'gtk-update-icon-cache')
makedepends=('mate-common>=1.15')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('ad4357c0a2e2d5c61b601b4b8fddbd409b82186f')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${_pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${_pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-static \
        --disable-python \
        --enable-locations-compression
    make
}

package_libmateweather() {
    pkgdesc+=' (GTK3 version [EXPERIMENTAL])'
    cd "${srcdir}/${_pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
