# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.15
_pkgbase=mate-sensors-applet
pkgname=(${_pkgbase})
pkgver=${_ver}.0
pkgrel=1
pkgdesc="A MATE Panel applet to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
provides=("${_pkgbase}" "${_pkgbase}-gtk3")
conflicts=("${_pkgbase}-gtk3")
license=('GPL')
depends=('dbus' 'glib2' 'gtk-update-icon-cache' 'libatasmart' 'libnotify'
         'lm_sensors' 'mate-panel>=1.15')
makedepends=('docbook-xml' 'libxnvctrl' 'mate-common' 'rarian' 'yelp-tools' 'gtk3' 'mate-panel>=1.15')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('c0c0b0029dcc3e6be88841912cfb275a1263bd4d')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${_pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${_pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --with-gtk=3.0 \
        --disable-static
    make
}

package_mate-sensors-applet() {
    pkgdesc+=' (GTK3 version)'
    cd "${srcdir}/${_pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
