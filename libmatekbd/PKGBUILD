# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.14
pkgbase=libmatekbd
pkgname=(${pkgbase} ${pkgbase}-gtk3)
pkgver=${_ver}.1
pkgrel=1
pkgdesc="MATE keyboard library"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('dconf' 'glib2' 'gtk2' 'gtk3' 'iso-codes' 'libxklavier')
makedepends=('mate-common')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('71fcb3a85ead07dcbb7f4ac3516590dd03ba483f')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk2"
    cp -a "${pkgbase}-gtk2" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk2"
    ./configure \
        --prefix=/usr \
        --with-gtk=2.0 \
        --disable-static
    make

    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --with-gtk=3.0 \
        --disable-static
    make
}

package_libmatekbd() {
    pkgdesc+=' (GTK2 version)'
    conflicts=("${pkgbase}-gtk3")
    depends=('dconf' 'gtk2' 'iso-codes' 'libxklavier')
    provides=('libmatekbd=1.14.1')
    
    cd "${srcdir}/${pkgbase}-gtk2"
    make DESTDIR="${pkgdir}" install
}

package_libmatekbd-gtk3() {
    pkgdesc+=' (GTK3 version [EXPERIMENTAL])'
    conflicts=("${pkgbase}")
    depends=('dconf' 'gtk3' 'iso-codes' 'libxklavier')
    provides=('libmatekbd=1.14.1')
    
    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
