# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.14
pkgbase=eom
pkgname=(${pkgbase} ${pkgbase}-gtk3)
pkgver=${_ver}.1
pkgrel=1
pkgdesc="An image viewing and cataloging program for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'glib2' 'gobject-introspection-runtime'
         'gtk2' 'gtk-update-icon-cache' 'exempi' 'lcms2' 'libexif' 'libjpeg-turbo'
         'librsvg' 'mate-desktop>=1.14' 'pygtk' 'python2-gobject2'
         'startup-notification' 'zlib')
makedepends=('gobject-introspection' 'mate-common' 'yelp-tools' 'gtk3' 'mate-desktop-gtk3>=1.14')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra')
conflicts=("${pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('df3e91693122053e97a87ca02119b2deda74e475')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk2"
    cp -a "${pkgbase}-gtk2" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk2"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --with-librsvg \
        --enable-python
    make

    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --with-librsvg \
        --disable-python
    make
}

package_eom() {
    pkgdesc+=' (GTK2 version)'
    conflicts=("${pkgbase}-gtk3")
    depends=('dbus-glib' 'desktop-file-utils' 'glib2' 'gobject-introspection-runtime'
             'gtk2' 'gtk-update-icon-cache' 'exempi' 'lcms2' 'libexif' 'libjpeg-turbo'
             'librsvg' 'mate-desktop>=1.14' 'pygtk' 'python2-gobject2'
             'startup-notification' 'zlib')

    cd "${srcdir}/${pkgbase}-gtk2"
    make DESTDIR="${pkgdir}" install
}

package_eom-gtk3() {
    pkgdesc+=' (GTK3 version)'
    conflicts=("${pkgbase}")
    depends=('dbus-glib' 'desktop-file-utils' 'glib2' 'gobject-introspection-runtime' 'gtk3'
             'gtk-update-icon-cache' 'exempi' 'lcms2' 'libexif' 'libjpeg-turbo' 'librsvg'
             'mate-desktop-gtk3>=1.14' 'startup-notification' 'zlib')

    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
