# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.14
_pkgbase=eom
pkgname=(${_pkgbase})
pkgver=${_ver}.1
pkgrel=1
pkgdesc="An image viewing and cataloging program for MATE (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gobject-introspection-runtime'
         'gtk2' 'exempi' 'lcms2' 'libexif' 'libjpeg-turbo' 'librsvg'
         'mate-desktop>=1.14' 'pygtk' 'python2-gobject2'
         'startup-notification' 'zlib')
makedepends=('gobject-introspection' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra')
conflicts=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('df3e91693122053e97a87ca02119b2deda74e475')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --with-librsvg \
        --enable-python
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
