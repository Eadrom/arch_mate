# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.14
_pkgbase=mate-terminal
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="The MATE Terminal Emulator (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus' 'dconf' 'glib2' 'libsm' 'mate-desktop-gtk3>=1.14' 'vte3')
makedepends=('docbook2x' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('a1b998334ac48147997ac0b596c40e4760edecf1')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --with-gtk=3.0 \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
