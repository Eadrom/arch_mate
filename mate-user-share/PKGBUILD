# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.15
pkgbase=mate-user-share
pkgname=(${pkgbase})
pkgver=${_ver}.0
pkgrel=1
pkgdesc="User level public file sharing via WebDAV for MATE."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
conflicts=("${pkgbase}-gtk3")
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'dconf' 'glib2' 'gtk-update-icon-cache'
         'libunique3' 'mod_dnssd')
makedepends=('caja>=1.15' 'libcanberra' 'libnotify' 'mate-common' 'yelp-tools' 'libunique3')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('149527ef51c89e5838efd0939204a5a5bd66e605')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk3"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=3.0 \
        --disable-static \
        --disable-bluetooth
    make
}

package_mate-user-share() {
    pkgdesc+=' (GTK3 version)'
    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/mate-user-share/dav_user_2.0.conf"
}
