# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.15
pkgbase=mate-notification-daemon
pkgname=(${pkgbase})
pkgver=${_ver}.0
pkgrel=1
provides=('notification-daemon')
pkgdesc="Notification daemon for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('dconf' 'dbus-glib' 'glib2' 'gtk3' 'gtk-update-icon-cache'
         'libcanberra' 'libwnck' 'libwnck3' 'libnotify')
makedepends=('mate-common')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('97b49509dd798ad3e53fb5782c7cec1971190e61')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --with-gtk=3.0 \
        --disable-static
    make
}

package_mate-notification-daemon() {
    pkgdesc+=' (GTK3 version [EXPERIMENTAL])'
    groups=('mate')
    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
