# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.15
pkgbase=mate-applets
pkgname=(${pkgbase})
pkgver=${_ver}.0
pkgrel=1
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64')
conflicts=("${pkgbase}-gtk3")
url="http://mate-desktop.org"
license=('GPL')
depends=('glib2' 'gtksourceview2' 'gtk-update-icon-cache' 'libgtop' 'libnotify'
         'mate-panel>=1.15' 'polkit' 'python2-dbus' 'python2-gobject' 'upower')
makedepends=('docbook2x' 'docbook-xsl' 'mate-common' 'yelp-tools' 'gtksourceview3' 'mate-panel>=1.15')
optdepends=('fortune-mod: for displaying fortune cookies in the Wanda the Fish applet'
            'yelp: for reading MATE help documents')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('4d6534cb83ec88fe247f8550e518977d10938640')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk3"
    cd "${srcdir}/${pkgbase}-gtk3"
    sed -i 's/env python/env python2/' invest-applet/invest/{chart.py,invest-applet.py,mate-invest-chart}
}

build() {
    cd "${srcdir}/${pkgbase}-gtk3"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgbase} \
        --enable-polkit \
        --enable-ipv6 \
        --with-gtk=3.0 \
        --disable-static
    make
}

package_mate-applets() {
    pkgdesc+=' (GTK3 version)'
    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
