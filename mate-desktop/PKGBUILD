# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.15
pkgbase=mate-desktop
pkgname=(${pkgbase})
pkgver=${_ver}.0
pkgrel=1
pkgdesc="Library with common API for various MATE modules"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
conflicts=("${pkgbase}-gtk3")
license=('GPL' 'LGPL')
depends=('dconf' 'gtk3' 'startup-notification')
makedepends=('gobject-introspection' 'mate-common' 'yelp-tools')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('db70bbc9c3439214922e66c5eb9c1fc2ca00b7a1')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --with-gtk=3.0 \
        --disable-static \
        --disable-schemas-compile \
        --enable-gtk-doc
    make
}

package_mate-desktop() {
    pkgdesc+=' (GTK3 version)'
    groups=('mate')
    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
