# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.15
pkgbase=marco
pkgname=(${pkgbase})
pkgver=${_ver}.0
pkgrel=1
pkgdesc="A window manager for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glib2' 'libcanberra' 'libgtop' 'libice' 'libsm' 'libxt'
         'mate-desktop>=1.15' 'startup-notification' 'zenity')
makedepends=('mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('44e29286538caac21597e543e208bd99254e7b9a')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --enable-startup-notification \
        --disable-static
    make
}

package_marco() {
    pkgdesc+=' (GTK3 version)'
    conflicts=("${pkgbase}")
    depends=('gtk3' 'glib2' 'libcanberra' 'libgtop' 'libice' 'libsm' 'libxt'
             'mate-desktop>=1.15' 'startup-notification' 'zenity')

    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
