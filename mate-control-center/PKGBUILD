# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.15
pkgbase=mate-control-center
pkgname=(${pkgbase})
pkgver=${_ver}.0
pkgrel=1
pkgdesc="The Control Center for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'desktop-file-utils' 'glib2' 'gsettings-desktop-schemas'
         'gtk3' 'gtk-update-icon-cache' 'mate-desktop>=1.15' 'mate-menus>=1.15'
         'mate-settings-daemon>=1.15' 'marco>=1.15' 'libgtop' 
         'libmatekbd>=1.15' 'libunique3' 'librsvg' 'libxss' 'libxt'
         'shared-mime-info' 'startup-notification')
makedepends=('docbook2x' 'mate-common>=1.15' 'yelp-tools' 'libmatekbd' 'libunique3'
             'marco' 'mate-desktop>=1.15' 'mate-settings-daemon')
optdepends=('yelp: for reading MATE help documents')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('a2715ee56e9ffad3a7a79090e25b5bdb058c85f3')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-static \
        --disable-update-mimedb
    make
}

package_mate-control-center() {
    pkgdesc+=' (GTK3 version)'

    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
