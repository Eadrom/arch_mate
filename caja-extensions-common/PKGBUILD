# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.15
_pkgbase=caja-extensions
pkgname=(caja-extensions-common caja-gksu caja-image-converter caja-open-terminal caja-sendto caja-share)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="MATE extensions for Caja (GTK3 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
provides=("${_pkgbase}" "${_pkgbase}-gtk3")
conflicts=("${_pkgbase}" "${_pkgbase}-gtk3")
license=('GPL')
depends=('caja>=1.15' 'dbus-glib' 'gksu' 'gtk3' 'gupnp' 'imagemagick' 'samba')
makedepends=('mate-common' 'caja')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('f4299e99fe889038834ab9743cf6418f01a777cc')

prepare() {
    cd "${srcdir}"
    mv "${_pkgbase}-${pkgver}" "${_pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${_pkgbase}-gtk3"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --with-gtk=3.0
    make
}

package_caja-extensions-common() {
    pkgdesc="Caja extensions (common files)"
    cd "${srcdir}/${_pkgbase}-gtk3"
    make -C po DESTDIR="${pkgdir}" install
}

package_caja-gksu() {
    pkgdesc="Privilege granting extension for Caja using gksu (GTK3 version [EXPERIMENTAL])"
    depends=('caja' 'caja-extensions-common' 'gksu')
    groups=('mate-extra')
    conflicts=('caja-gksu' 'caja-gksu-gtk3')

    cd "${srcdir}/${_pkgbase}-gtk3"
    make -C gksu DESTDIR="${pkgdir}" install
}

package_caja-image-converter() {
    pkgdesc="A Caja extension for simple image conversions (GTK3 version [EXPERIMENTAL])"
    depends=('caja' 'caja-extensions-common' 'imagemagick')
    groups=('mate-extra')
    conflicts=('caja-image-converter' 'caja-image-converter-gtk3')

    cd "${srcdir}/${_pkgbase}-gtk3"
    make -C image-converter DESTDIR="${pkgdir}" install
}

package_caja-open-terminal() {
    pkgdesc="A Caja extension for opening terminals in arbitrary local paths (GTK3 version [EXPERIMENTAL])"
    depends=('caja' 'caja-extensions-common' 'glib2')
    groups=('mate-extra')
    conflicts=('caja-open-terminal' 'caja-open-terminal-gtk3')

    cd "${srcdir}/${_pkgbase}-gtk3"
    make -C open-terminal DESTDIR="${pkgdir}" install
}

package_caja-sendto() {
    pkgdesc="A Caja extension for sending files (GTK3 version [EXPERIMENTAL])"
    depends=('caja' 'caja-extensions-common' 'dbus-glib' 'glib2' 'gupnp')
    groups=('mate-extra')
    optdepends=('pidgin: Support for sending to instant messaging networks.'
                'gajim: Support for sending to Jabber.')
    conflicts=('caja-sendto' 'caja-sendto-gtk3')

    cd "${srcdir}/${_pkgbase}-gtk3"
    make -C sendto DESTDIR="${pkgdir}" install
}

package_caja-share() {
    pkgdesc="A Caja extension to quickly share a folder (GTK3 version [EXPERIMENTAL])"
    depends=('caja' 'caja-extensions-common' 'samba')
    groups=('mate-extra')
    conflicts=('caja-share' 'caja-share-gtk3')

    cd "${srcdir}/${_pkgbase}-gtk3"
    make -C share DESTDIR="${pkgdir}" install
}
