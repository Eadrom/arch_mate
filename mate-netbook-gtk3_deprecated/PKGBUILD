# Maintainer:  eadrom <eadrom@archlinux.info>
# Contributor:  Martin Wimpress <code@flexion.org>

_ver=1.14
_pkgbase=mate-netbook
pkgname=${_pkgbase}-gtk3
pkgver=${_ver}.0
pkgrel=1
pkgdesc="A simple window management tool (GTK3 version [EXPERIMENTAL])."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glib2' 'gtk3' 'libfakekey' 'libwnck3' 'libunique3' 'mate-panel-gtk3>=1.14')
makedepends=('mate-common')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
conflicts=("${_pkgbase}")
sha1sums=('9a26877c8c2707f5c3a5744f9fb74a7cc6d49b55')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=3.0 \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
